grammar dk.sdu.mmmi.embedix.ULSWIG with org.eclipse.xtext.common.Terminals

generate ulswig "http://www.sdu.dk/mmmi/embedix/ULSWIG"

Robot:
	constructors+=Constructor*;
	
Constructor:
	(isPublic?="public")? name=ID '(' (parameters+=ID (',' parameters += ID)*)? ')' 
	(':' addresses+=Address (',' addresses+=Address)*)? '{' (members+=Member ';')* '}'
;

Address:
	'@' name=ID |
	'@' '(' elements+=ID (',' elements+=ID)* ')'
;

Member:
	LinkBinding | Expansion | Instantiation | Grouping
;

// Link binding

LinkBinding:
	'TOSNET' '(' (properties+=LinkProperty (',' properties+=LinkProperty)) ')'
;

LinkProperty:
	'LINK' '=' linkName=ID |
	'BASE' '=' baseValue=INT
;

// Expansion (application of a constructor)

Expansion:
	identifier=ExpansionName ':' constructor=[Constructor] '(' (arguments+=Argument (',' arguments+=Argument)*)? ')'
	(bindings+=AddressBinding (',' bindings+=AddressBinding)*)?
;

ExpansionName:
	SimpleExpansionName |
	AddressExpansionName 
;

SimpleExpansionName:
	name=ID
;

AddressExpansionName:
	'@' name=ID
;

Argument:
	simple=ID |
	lhs=ID '+' rhs=Argument |
	text=STRING	
;

AddressBinding:
	'@' name=ID '=' '(' addresses+=HEX_NUM (',' addresses+=HEX_NUM)* ')'
;

// Instantiation of proxy

Instantiation:
	'@' address=AddressSpec ':' kind=Kind '(' (properties+=InstantiationProperty (',' properties+=InstantiationProperty)*)? ')'
;

AddressSpec:
	DirectAddressSpec |
	NamedAddressSpec
;

DirectAddressSpec:
	name=ID '=' address=HEX_NUM
;

NamedAddressSpec:
	name=ID
;

Kind:
	'READ' |
	'WRITE'
;

InstantiationProperty:
	{IDProperty} 'ID' '=' id=Argument |
	{CRCProperty} 'CRC' |
	{PublishPoperty} 'PUBLISH' '(' mode=INT ',' rate=INT ')'
;

// Grouping of existing elements (alias)

Grouping:
	name=ID '=' elements+=GroupElement (',' elements+=GroupElement)*
;

GroupElement:
	path+=PathElement ('.' path+=PathElement)*
;

PathElement:
	simple=ID |
	'ALL' '(' type=[Constructor] ')'	
;

// Hexadecimal numbers (lexer rule)

terminal HEX_NUM : '0' 'x' ('0'..'9'|'a'..'f'|'A'..'F')+;

